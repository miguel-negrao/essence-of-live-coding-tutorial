{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    ":set -XArrows\n",
    ":set -XDeriveDataTypeable\n",
    ":set -XFlexibleContexts\n",
    ":set -XOverloadedStrings\n",
    "\n",
    "-- ghcid \"--command=stack ghci Main.hs\"\n",
    "\n",
    "-- base\n",
    "import Control.Arrow\n",
    "import Control.Monad\n",
    "import Control.Monad.IO.Class\n",
    "import Control.Monad.Fix (MonadFix)\n",
    "import Data.Foldable\n",
    "import Data.Function ((&))\n",
    "import Data.Functor\n",
    "import Data.Maybe\n",
    "import Text.Read (readMaybe)\n",
    "\n",
    "\n",
    "-- vector-space\n",
    "import Data.VectorSpace\n",
    "\n",
    "-- utf8-string\n",
    "import Data.ByteString.UTF8\n",
    "\n",
    "-- essence-of-live-coding\n",
    "import LiveCoding hiding (integrate)\n",
    "\n",
    "-- essence-of-live-coding-gloss\n",
    "import LiveCoding.Gloss\n",
    "\n",
    "-- essence-of-live-coding-pulse\n",
    "import LiveCoding.Pulse\n",
    "\n",
    "-- essence-of-live-coding-warp\n",
    "import LiveCoding.Warp\n",
    "\n",
    "-- * Main program\n",
    "\n",
    "main :: IO ()\n",
    "main = runHandlingStateT $ foreground liveProgram\n",
    "\n",
    "-- Uncomment the different *RunCells to start different media backends!\n",
    "liveProgram :: LiveProgram (HandlingStateT IO)\n",
    "liveProgram = liveCell $ proc _ -> do\n",
    "  -- warpRunCell  -< ()\n",
    "  glossRunCell -< ()\n",
    "  -- pulseRunCell -< ()\n",
    "  returnA      -< ()\n",
    "\n",
    "-- * Warp subcomponent\n",
    "\n",
    "-- | Starts a webserver on port 8080\n",
    "warpRunCell :: Cell (HandlingStateT IO) () (Maybe RequestInfo)\n",
    "warpRunCell = runWarpC 8080 warpCell\n",
    "\n",
    "-- | This handles the incoming request from the webserver\n",
    "warpCell :: Cell IO ((), Request) (RequestInfo, Response)\n",
    "warpCell = proc ((), request) -> do\n",
    "  body <- arrM lazyRequestBody -< request\n",
    "  returnA -< (getRequestInfo request, emptyResponse)\n",
    "\n",
    "-- | The type of interesting data from the request\n",
    "type RequestInfo = Query\n",
    "\n",
    "-- | Extract data from the request to use in the rest of the program\n",
    "getRequestInfo :: Request -> RequestInfo\n",
    "getRequestInfo = queryString\n",
    "\n",
    "-- Extend this for a more interesting website\n",
    "emptyResponse :: Response\n",
    "emptyResponse = responseLBS\n",
    "  status200\n",
    "  [(\"Content-Type\", \"text/plain\")]\n",
    "  \"Yep, it's working\"\n",
    "\n",
    "-- * Gloss subcomponent\n",
    "\n",
    "-- ** Backend setup\n",
    "\n",
    "borderX :: Num a => a\n",
    "borderX = 300\n",
    "\n",
    "borderY :: Num a => a\n",
    "borderY = 400\n",
    "\n",
    "border :: Num a => (a, a)\n",
    "border = (borderX, borderY)\n",
    "\n",
    "glossSettings :: GlossSettings\n",
    "glossSettings = defaultSettings\n",
    "  { debugEvents = True\n",
    "  , displaySetting = InWindow \"Essence of Live Coding Tutorial\" (border ^* 2) (0, 0)\n",
    "  }\n",
    "\n",
    "-- | Run the gloss backend at 30 frames per second\n",
    "glossRunCell :: Cell (HandlingStateT IO) () (Maybe ())\n",
    "glossRunCell = glossWrapC glossSettings $ glossCell\n",
    "  -- & (`withDebuggerC` statePlay) -- Uncomment to display the internal state\n",
    "\n",
    "-- ** Main gloss cell\n",
    "\n",
    "-- | This cell is called for every frame of the graphics output\n",
    "glossCell :: Cell PictureM () ()\n",
    "glossCell = proc () -> do\n",
    "  events <- constM ask -< ()\n",
    "  ball <- ballSim -< events\n",
    "  addPicture -< ballPic ball\n",
    "  returnA    -< ()\n",
    "\n",
    "-- ** Ball\n",
    "\n",
    "ballRadius :: Num a => a\n",
    "ballRadius = 20\n",
    "\n",
    "-- | Draw the ball in gloss\n",
    "ballPic :: Ball -> Picture\n",
    "ballPic Ball { ballPos = (x, y) } = translate x y $ color white $ thickCircle (ballRadius / 2) ballRadius\n",
    "\n",
    "-- | The type of internal state of the 'ballSim'\n",
    "data Ball = Ball\n",
    "  { ballPos :: (Float, Float)\n",
    "  , ballVel :: (Float, Float)\n",
    "  } deriving Data\n",
    "\n",
    "ballPosX = fst . ballPos\n",
    "ballPosY = snd . ballPos\n",
    "ballVelX = fst . ballVel\n",
    "ballVelY = snd . ballVel\n",
    "\n",
    "-- | Simulate the position of the ball, given recent events such as mouse clicks\n",
    "ballSim :: (Monad m, MonadFix m) => Cell m [Event] Ball\n",
    "ballSim = proc events -> do\n",
    "  rec\n",
    "    let accMouse = sumV $ (^-^ ballPos ball) <$> clicks events\n",
    "        accCollision = sumV $ catMaybes\n",
    "          [ guard (ballPosX ball < - borderX + ballRadius && ballVelX ball < 0)\n",
    "              $> (-2 * ballVelX ball, 0)\n",
    "          , guard (ballPosX ball >   borderX - ballRadius && ballVelX ball > 0)\n",
    "              $> (-2 * ballVelX ball, 0)\n",
    "          , guard (ballPosY ball < - borderY + ballRadius && ballVelY ball < 0)\n",
    "              $> (0, -2 * ballVelY ball)\n",
    "          , guard (ballPosY ball >   borderY - ballRadius && ballVelY ball > 0)\n",
    "              $> (0, -2 * ballVelY ball)\n",
    "          ]\n",
    "    frictionVel <- integrate -< (-5) *^ ballVel ball\n",
    "    impulses <- sumS -< sumV [accMouse, 0.97 *^ accCollision]\n",
    "    let newVel = frictionVel ^+^ impulses\n",
    "    newPos <- integrate -< newVel\n",
    "    let ball = Ball newPos newVel\n",
    "  returnA -< ball\n",
    "\n",
    "-- | Extract the positions of left mouse clicks\n",
    "clicks :: [Event] -> [(Float, Float)]\n",
    "clicks = mapMaybe click\n",
    "\n",
    "click :: Event -> Maybe (Float, Float)\n",
    "click (EventKey (MouseButton LeftButton) Down _ pos) = Just pos\n",
    "click _ = Nothing\n",
    "\n",
    "-- * Pulse subcomponent\n",
    "\n",
    "-- | Run the PulseAudio backend at 48000 samples per second\n",
    "pulseRunCell :: Cell (HandlingStateT IO) () [()]\n",
    "pulseRunCell = pulseWrapC 1600 $ arr (const 440) >>> sawtooth >>> addSample\n",
    "\n",
    "-- * Utilities\n",
    "\n",
    "sumS\n",
    "  :: (Monad m, Data v, VectorSpace v)\n",
    "  => Cell m v v\n",
    "sumS = foldC (^+^) zeroV\n",
    "\n",
    "integrate\n",
    "  :: (Monad m, Data v, VectorSpace v, Fractional (Scalar v))\n",
    "  => Cell m v v\n",
    "integrate = arr (^/ fromIntegral (stepsPerSecond glossSettings)) >>> sumS\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "import LiveCoding.GHCi \n",
    "import Control.Concurrent\n",
    "\n",
    "launchedProgram <- launch liveProgram\n",
    "save launchedProgram"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "output_type": "display_data",
     "data": {
      "text/plain": ""
     },
     "metadata": {}
    }
   ],
   "source": [
    "launchedProgram <- launch liveProgram\n",
    "update launchedProgram liveProgram"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "mimetype": "text/x-haskell",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "8.8.4"
  },
  "orig_nbformat": 2
 },
 "nbformat": 4,
 "nbformat_minor": 2
}